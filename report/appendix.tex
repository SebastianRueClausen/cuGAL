\section{Appendix}
%\subsection{email-Enron Dataset}\label{enron_experiment}
%\subsection{clamped \textsc{FUGAL}}
%\resultplot{caAstroPh}{data/res_oneway_caAstroph_accu.csv}{data/res_oneway_caAstroph_time.csv}{12}{false}
%\resultplot{caHep}{data/res_oneway_caHep_accu.csv}{data/res_oneway_caHep_time.csv}{12}{true}
%\resultplot{enron}{data/res_enron_accu.csv}{data/res_enron_time.csv}{12}{true}

\subsection{Sinkhorn-Knopp-Log CUDA Implementation}\label{sinkhorn_log_impl}
\begin{lstlisting}
#include <cuda.h>
#include <cuda_runtime.h>
#include <cuda_fp16.h>
#include <torch/torch.h>
#include <c10/cuda/CUDAGuard.h>
#include "common.cuh"

template <typename scalar_t, size_t dims>
using Accessor = torch::PackedTensorAccessor32<scalar_t, dims>;

constexpr int div_ceil(int x, int y) {
    return (x + y - 1) / y;
}

// Performs a sum reduction within a single warp.
__device__ inline float warp_sum_reduce(float sum) {
#pragma unroll
    for (auto offset = warpSize / 2; offset > 0; offset >>= 1) {
        sum += __shfl_xor_sync(__activemask(), sum, offset);
    }
    return sum;
}

// Performs a max reduction within a single warp.
__device__ inline float warp_max_reduce(float max) {
#pragma unroll
    for (auto offset = warpSize / 2; offset > 0; offset >>= 1) {
        max = fmaxf(max, __shfl_xor_sync(__activemask(), max, offset));
    }
    return max;
}

// | w/l | 0 | 1 | 2 |
// |-----|---|---|---|
// | 0   | a | a | a |
// | 1   | b | b | b |
// | 2   | c | c | c |
//
//         |
//         v
//
// | w/l | 0 | 1 | 2 |
// |-----|---|---|---|
// | 0   | a | b | c |
// | 1   | a | b | c |
// | 2   | a | b | c |
template <typename scalar_t>
__device__ inline scalar_t warp_lane_swap(scalar_t value, scalar_t default_value) {
    const auto lane = threadIdx.x % warpSize;
    const auto warp = threadIdx.x / warpSize;

    __shared__ scalar_t shared[32];

    if (lane == 0) {
        shared[warp] = value;
    }

    __syncthreads();

    if (lane < blockDim.x / warpSize) {
        return shared[lane];
    } else {
        return default_value;
    }
}

// Performs a max reduction across multiple warps in a block.
__device__ inline float block_sum_reduce(float value) {
    return warp_sum_reduce(warp_lane_swap<float>(warp_sum_reduce(value), 0.0));
}

// Performs a max reduction across multiple warps in a block.
__device__ inline float block_max_reduce(float value) {
    return warp_max_reduce(warp_lane_swap<float>(warp_max_reduce(value), -INFINITY));
}

// Adds `add` to each column of `K` and sums all rows together.
__global__ void kernel(
    const Accessor<float, 2> K,
    const Accessor<float, 1> add,
    Accessor<float, 1> out,
    const size_t size
) {
    const auto tid = threadIdx.x;
    const auto bid = blockIdx.x;

    float max = -INFINITY;
    float sum = 0.0;

    #pragma unroll
    for (auto i = tid; i < size; i += blockDim.x) {
        max = fmaxf(max, K[bid][i] + add[i]);
    }

    max = block_max_reduce(max);

    #pragma unroll
    for (auto i = tid; i < size; i += blockDim.x) {
        sum += expf(K[bid][i] + add[i] - max);
    }

    sum = block_sum_reduce(sum);

    if (tid == 0) {
        out[bid] = -(max + logf(sum));
    }
}

constexpr size_t block_size = 32 * 12;

void sinkhorn_step_cuda(torch::Tensor K, torch::Tensor add, torch::Tensor out) {
    at::cuda::CUDAGuard device_guard(K.device());
    const auto blocks = K.size(0);
    kernel<<<blocks, block_size>>>(
        K.packed_accessor32<float, 2>(),
        add.packed_accessor32<float, 1>(),
        out.packed_accessor32<float, 1>(),
        K.size(0)
    );
    cudaDeviceSynchronize();
}
\end{lstlisting}